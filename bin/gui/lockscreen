#!/bin/zsh

source "$DOTFILE_DIR/theme/solarized-dark/colors"

declare -a wallpaper_args
if [[ -n "$WALLPAPER_BLURRED" ]]; then
    wallpaper_args=('-i' "$WALLPAPER_BLURRED")
else
    wallpaper_args=()
fi
pkill i3lock

spotifyctl=(playerctl -p spotify)
original_spotify_status=$($spotifyctl status)
# Sending SIGUSR1 to dunst will prevent it from displaying notifications.
# SIGUSR2 resumes notifications again.
#
# Suspending notifications is handy when the screen is locked!
pkill -SIGUSR1 dunst

{
    if [[ "$SCREENSAVER_STRATEGY" == 'system-suspend' ]]; then
        sleep 300
        sudo systemctl suspend
    elif [[ "$SCREENSAVER_STRATEGY" == 'screen-off' ]]; then
        sleep 30
        xset dpms force off
    fi
} &

lock_time=$(date '+%s')
if [[ "$original_spotify_status" == 'Playing' ]]; then
    $spotifyctl pause
fi
i3lock -n "${wallpaper_args[@]}" -c "$(echo "$SOLARIZED_BASE03" | sed -e 's/^#//')"
unlock_time=$(date '+%s')
if [[ "$original_spotify_status" == 'Playing' && "$((unlock_time - lock_time))" -lt "$SCREENLOCK_MUSIC_RESUME_TIME_SECONDS" ]]; then
    $spotifyctl play
fi
pkill -SIGUSR2 dunst

# Kill the backgrounded job responsible for turning off the screen.
# If the screen hasn't shut off yet but the session has been unlocked,
# the screen could flash off if the background job were allowed to
# run.
kill %1
