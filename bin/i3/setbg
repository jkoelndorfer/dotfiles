#!/bin/zsh

script_dir=$(dirname "$0")
cd "$script_dir"

source "$DOTFILE_DIR/theme/solarized-dark/colors"
source "$DOTFILE_DIR/theme/solarized-dark/wallpaper"
source "$DOTFILE_DIR/lib/sh/gui.sh"
source "$DOTFILE_DIR/lib/sh/wallpaper.sh"

wallpaper_path=$(get_wallpaper_path)
wallpaper_cache_dir=$(get_wallpaper_cache_dir)

function create_blurred_bg() {
    local wallpaper_path=$1
    local outputs=$2

    local blur_args=(-blur 0x30)
    local blurred_fullsize="${wallpaper_cache_dir}/${WALLPAPER}_blurred"

    if [[ ! -f "$blurred_fullsize" ]]; then
        convert "${blur_args[@]}" "$wallpaper_path" "$blurred_fullsize"
    fi

    if [[ -z "$outputs" ]]; then
        return
    fi

    echo "$outputs" | while read o; do
        local bg_path=$(output_info 'bg-path' "$o")
        local blurred_bg_path=$(output_info 'blurred-bg-path' "$o")
        if [[ ! -f "$blurred_bg_path" ]]; then
            convert "${blur_args[@]}" "$bg_path" "$blurred_bg_path"
        fi
    done
}

function set_sway_bg() {
    local outputs=$1

    echo "$outputs" | while read o; do
        local output_name=$(output_info 'output-name' "$o")
        local bg_path=$(output_info 'bg-path' "$o")
        swaybg --output "$output_name" --image "$bg_path" --color "$DEFAULT_DESKTOP_BG_COLOR" & disown
    done
}

# Sway requires that we define the background of each output separately.
function split_bg_for_outputs() {
    local wallpaper_path=$1
    local outputs=$2

    echo "$outputs" | while read o; do
        local crop_parameters=$(output_info 'bg-crop-parameters' "$o")
        local cropped_bg_path=$(output_info 'bg-path' "$o")

        if [[ ! -f "$cropped_bg_path" ]]; then
            convert -crop "$crop_parameters" "$wallpaper_path" "$cropped_bg_path"
        fi
    done
}

mkdir -p "$wallpaper_cache_dir"

if [[ -n "$WAYLAND_DISPLAY" ]]; then
    outputs=$(get_sway_outputs)
    split_bg_for_outputs "$wallpaper_path" "$outputs"
    create_blurred_bg "$wallpaper_path" "$outputs"
    kill_processes_in_display swaybg
    set_sway_bg "$outputs"
else
    # `xsetroot -solid` does not work with compton. See https://github.com/chjj/compton/issues/162.
    # Rather than use the suggested hsetroot, which is an AUR package, we trick feh into doing what
    # we want by providing a 1x1 transparent background and giving it out desired background color.
    # feh is needed for setting proper wallpapers, anyhow. Why use two utilities when you can keep
    # it to one?
    if [[ -n "$wallpaper_path" && -f "$wallpaper_path" ]]; then
        feh --no-fehbg --bg-tile "$wallpaper_path" --image-bg "$DEFAULT_DESKTOP_BG_COLOR" --no-xinerama
        create_blurred_bg "$wallpaper_path" ''
    else
        feh --no-fehbg --bg-center "$DOTFILE_DIR/general/1x1-transparent.png" --image-bg "$DEFAULT_DESKTOP_BG_COLOR" --no-xinerama
    fi
fi
